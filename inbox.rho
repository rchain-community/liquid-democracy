new Inbox,
  deployId(`rho:rchain:deployId`),
  stdout(`rho:io:stdout`) in {
stdout!("begin") |
contract Inbox(capabilities) = {
  new read, write, peek, box in {
    capabilities!(bundle+{*read}, bundle+{*write}, bundle+{*peek}) |
    box!(Nil) |

    contract read(ret) = {
      for (items <<- box) {
        ret!(*items)
      }
    } |
    contract read(type,ret) = {
      //stdout!(["type",*type]) |
      for (items <- box) {
        match (*items) {
          {[=*type, ...item] | rest} => {
            //stdout!(item) |
            ret!(item) |
            box!(rest)
          }
          _ => {box!(*items) | ret!(Nil) | stdout!([*type,"not found"])}
        }
      }
    } |
    contract read(type,subtype,ret) = {
      //stdout!(["type",*type]) |
      for (items <- box) {
        match (*items) {
          {[=*type, =*subtype, ...item] | rest} => {
            //stdout!(item) |
            ret!(item) |
            box!(rest)
          }
          _ => {
            box!(*items)|ret!(Nil) | stdout!([*type, *subtype, "not found"])
          }
        }
      }
    } |

    contract peek(ret) = {
      for (items <<- box) {
        ret!(*items)
      }
    } |
    contract peek(type,ret) = {
      //stdout!(["type",*type]) |
      for (items <<- box) {
        match (*items) {
        // ... |["chat", "joes rev addr", {"date":222222 "content": "hello from joe" }] | ...
          {[=*type, ...rest] | tail} => {
            //stdout!(rest) |
            ret!(rest)
          }
          _ => { ret!(Nil) | stdout!([*type,"not found"])}
        }
      }
    } |
    contract peek(type,subtype,ret) = {
      //stdout!(["type",*type]) |
      for (items <<- box) {
        match (*items) {
          {[=*type, =*subtype, ...item] | rest} => {
            //stdout!(item) |
            ret!(item) 
          }
          _ => {
           ret!(Nil) | stdout!([*type, *subtype, "not found"])
          }
        }
      }
    } |

    contract write(@message,ret) = {
       for (items <- box) {
         match message {
          [type, subtype, map /\ {a: b, ..._} ] => {
            match *items {
              {[=type, =subtype, oldmap] | more } => {
                box!([type, subtype, map.union(oldmap)] | more) |
           //stdout!([type, subtype, map.union(oldmap)]) |
                ret!("added.")
              }
              _ => {
                box!(*items | message) |
                ret!("sent...")
              }
            }
          }
          _ => {
            //stdout!([*message, *items]) |
            box!(*items | message) |
            ret!("sent.")
          }
        }
      }
    }
  }
} |
new capabilities, ack, ack1, ack2, ack3, ack4, ack5, message, uriCh, ret, insertArbitrary(`rho:registry:insertArbitrary`) in {
  stdout!("hello world") |
  insertArbitrary!(bundle+{*Inbox}, *uriCh)|
  for (@uri <- uriCh) {
    stdout!(["#define $Inbox", uri]) |
    deployId!(uri)
  } |
  Inbox!(*capabilities) |
  for (receive, send, peek <- capabilities) {
    stdout!("received capabilities") |
  insertArbitrary!(*send, *uriCh)|
    send!(["values","test", {"a": "b", "b": "c"}], *ack) |
    send!(["chat","myusername" ++ ": Watson come quickly!"], *ack1) |
    send!(["chat","myusername", "Watson come now!"], *ack1) |
    send!(["chat","hello world"], *ack) |
    for ( uri <- uriCh;  _ <- ack; _ <- ack1; _ <- ack2; _ <- ack3) {
      stdout!(["inbox",*uri]) | // for testing temprarily
      send!(["values","test", {"a": "x", "c": "d"}], *ret) |
      for ( map <- ret ) {peek!("values","test",*stdout)} |
      //receive!(["chat", _ ], *message) |
      receive!("chat", *message) |
      for ( text <- message ) {
        stdout!(*text)
      } |
      receive!("chat", "myusername", *message) |
      for ( text <- message ) {
        stdout!(*text)
      } |
      receive!("chat", *message) |
      for ( text <- message ) {
        stdout!(*text)
      }
    }
  }
}}